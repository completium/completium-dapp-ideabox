archetype ideasbox

constant chairman : address = @tz1Lc2qBKEWCBeDU8npG6zCeCqpmaegRi6Jg
constant expiration : date = 2020-12-22

states =
| Activated initial
| Terminated

asset idea {
  id       : nat;
  desc     : string;
  nbvotes  : nat = 0;
  creation : date;
  author   : address;
}

asset voter {
  addr      : address;
  remaining : nat = 5;
}

asset selected {
  sid : nat;
}

entry register (a_voter : address) {
  called by chairman
  require {
    r0 : state = Activated;
  }
  effect { voter.add({ addr = a_voter }) }
}

entry add_idea(description : string) {
  require {
    r1 : now <= expiration - 2d;
    r2 : state = Activated;
  }
  effect {
    idea.add({
			id = idea.count();
			desc = description;
			creation = now;
			author = caller
		})
  }
}

entry vote(n : nat, weight : nat) {
  require {
    r3 : voter.contains(caller);
    r4 : expiration - 2d < now <= expiration;
    r5 : voter[caller].remaining >= weight;
    r6 : state = Terminated;
  }
  effect {
    voter[caller].remaining -= weight;
    idea[n].nbvotes += weight;
  }
}

transition terminate () {
  called by chairman
  from Activated
  to Terminated
  with effect {
    for i in idea.select(the.nbvotes >= 5).sort(desc(nbvotes)).head(3) do
        selected.add({i})
    done
  }
}

// SPECIFICATION

specification entry vote (n : nat, weight : nat) {
	postcondition p1 {
      let some v = voter[caller] in
      let some bv = before.voter[caller] in
        v.remaining = bv.remaining - weight
      otherwise true otherwise true
    }
}

specification {
    i1 : 5 * voter.count() = idea.sum(nbvotes) + voter.sum(remaining)
}
